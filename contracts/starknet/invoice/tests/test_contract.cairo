use openzeppelin_presets::interfaces::ERC20UpgradeableABIDispatcherTrait;

use snforge_std::{
    ContractClassTrait, DeclareResultTrait, CheatSpan, EventSpyAssertionsTrait, start_cheat_block_timestamp_global
};
use core::starknet::{ContractAddress, contract_address_const};

use invoice_payment::{IInvoicePaymentDispatcherTrait, IInvoicePaymentDispatcher};
use openzeppelin_presets::interfaces::{ERC20UpgradeableABIDispatcher};
use openzeppelin_utils::serde::SerializedAppend;
use openzeppelin_token::erc20::ERC20Component;
use core::poseidon::hades_permutation;

pub const SUPPLY: u256 = 1_000_000_000_000_000_000; // 1e18

pub fn NAME() -> ByteArray {
    "NAME"
}

pub fn SYMBOL() -> ByteArray {
    "SYMBOL"
}

pub fn OWNER() -> ContractAddress {
    contract_address_const::<'owner'>()
}

pub fn SENDER() -> ContractAddress {
    contract_address_const::<'sender'>()
}

pub fn RECIPIENT() -> ContractAddress {
    contract_address_const::<'recipient'>()
}

pub fn setup_erc20(recipient: ContractAddress) -> ERC20UpgradeableABIDispatcher {
    let mut calldata = array![];

    calldata.append_serde(NAME());
    calldata.append_serde(SYMBOL());
    calldata.append_serde(SUPPLY);
    calldata.append_serde(recipient);
    calldata.append_serde(recipient);

    let contract = snforge_std::declare("ERC20Upgradeable").unwrap().contract_class();
    let (contract_address, _) = contract.deploy(@calldata).unwrap();

    ERC20UpgradeableABIDispatcher { contract_address: contract_address }
}

pub fn setup_invoice_payment() -> IInvoicePaymentDispatcher {
    let contract = snforge_std::declare("InvoicePayment").unwrap().contract_class();

    let (contract_address, _) = contract.deploy(@array![]).unwrap();

    IInvoicePaymentDispatcher { contract_address: contract_address }
}

fn simulate_payment(
    erc20_abi: ERC20UpgradeableABIDispatcher,
    invoice_payment_abi: IInvoicePaymentDispatcher,
    quote_id_hash: felt252,
    expiry: u64,
    amount: u256,
    recipient: ContractAddress,
) {
    snforge_std::cheat_caller_address(erc20_abi.contract_address, OWNER(), CheatSpan::TargetCalls(1));
    erc20_abi.transfer(SENDER(), amount);

    snforge_std::cheat_caller_address(erc20_abi.contract_address, SENDER(), CheatSpan::TargetCalls(1));
    erc20_abi.approve(invoice_payment_abi.contract_address, amount);

    snforge_std::cheat_caller_address(invoice_payment_abi.contract_address, SENDER(), CheatSpan::TargetCalls(1));
    invoice_payment_abi.pay_invoice(quote_id_hash, expiry, erc20_abi.contract_address, amount, recipient);
}

#[test]
fn it_works() {
    const AMOUNT: u256 = 200;
    let erc20_abi = setup_erc20(OWNER());
    let invoice_payment_abi = setup_invoice_payment();

    let quote_id_hash: felt252 = snforge_std::generate_random_felt();

    let mut spy = snforge_std::spy_events();

    let expiry = 0;

    simulate_payment(erc20_abi, invoice_payment_abi, quote_id_hash, expiry, AMOUNT, RECIPIENT());

    let (id_hash, _, _) = hades_permutation(quote_id_hash, expiry.into(), 2);

    // Payment went through
    assert_eq!(erc20_abi.balance_of(SENDER()), 0);
    assert_eq!(erc20_abi.balance_of(RECIPIENT()), AMOUNT);
    assert_eq!(erc20_abi.balance_of(OWNER()), SUPPLY - AMOUNT);

    // Event were emitted
    spy
        .assert_emitted(
            @array![
                (
                    erc20_abi.contract_address,
                    ERC20Component::Event::Transfer(
                        ERC20Component::Transfer { from: SENDER(), to: RECIPIENT(), value: AMOUNT },
                    ),
                ),
            ],
        );

    spy
        .assert_emitted(
            @array![
                (
                    invoice_payment_abi.contract_address,
                    invoice_payment::InvoicePayment::Event::Remittance(
                        invoice_payment::InvoicePayment::Remittance {
                            payee: RECIPIENT(),
                            asset: erc20_abi.contract_address,
                            invoice_id: id_hash,
                            amount: AMOUNT,
                            payer: SENDER(),
                        },
                    ),
                ),
            ],
        );
}


#[test]
fn good_invoice_id() {
    const AMOUNT: u256 = 200;
    let erc20_abi = setup_erc20(OWNER());
    let invoice_payment_abi = setup_invoice_payment();

    let quote_id_hash: felt252 = 5;

    let mut spy = snforge_std::spy_events();

    let expiry = 0;

    simulate_payment(erc20_abi, invoice_payment_abi, quote_id_hash, expiry, AMOUNT, RECIPIENT());
    
    // Id hash generated by the poseidon function in rust
    let id_hash: felt252 = 1061033641326993326300720138718993270320561547448486516072009162443017209979;
    spy
        .assert_emitted(
            @array![
                (
                    invoice_payment_abi.contract_address,
                    invoice_payment::InvoicePayment::Event::Remittance(
                        invoice_payment::InvoicePayment::Remittance {
                            payee: RECIPIENT(),
                            asset: erc20_abi.contract_address,
                            invoice_id: id_hash,
                            amount: AMOUNT,
                            payer: SENDER(),
                        },
                    ),
                ),
            ],
        );
}

#[test]
#[should_panic]
fn bad_expiry() {
    const AMOUNT: u256 = 200;
    let erc20_abi = setup_erc20(OWNER());
    let invoice_payment_abi = setup_invoice_payment();

    let quote_id_hash: felt252 = 5;

    let mut spy = snforge_std::spy_events();

    let expiry = 1;
    start_cheat_block_timestamp_global(3);

    simulate_payment(erc20_abi, invoice_payment_abi, quote_id_hash, expiry, AMOUNT, RECIPIENT());

    // Id hash generated by the poseidon function in rust
    let id_hash: felt252 = 1278717218625069357298452719407742240477041713675101626249510460722529827614;
    spy
        .assert_emitted(
            @array![
                (
                    invoice_payment_abi.contract_address,
                    invoice_payment::InvoicePayment::Event::Remittance(
                        invoice_payment::InvoicePayment::Remittance {
                            payee: RECIPIENT(),
                            asset: erc20_abi.contract_address,
                            invoice_id: id_hash,
                            amount: AMOUNT,
                            payer: SENDER(),
                        },
                    ),
                ),
            ],
        );
}